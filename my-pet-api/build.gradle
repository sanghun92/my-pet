plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

dependencies {
    // spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // db
    implementation 'mysql:mysql-connector-java'

    // modules
    implementation project(':my-pet-config')
    implementation project(':my-pet-domain')

    testImplementation 'io.rest-assured:rest-assured:5.3.0'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.google.guava:guava:14.0'

    // to fix [javax.annotation.meta.When not found]
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // to fix [ Could not write JSON: Java 8 date/time type `java.time.LocalDateTime` not supported]
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

tasks.test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.asciidoctor {
    configurations 'asciidoctorExt'
    dependsOn tasks.test
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
}

tasks.asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

tasks.register("copyDocument") {
    dependsOn tasks.asciidoctor
    copy {
        from "build/docs/asciidoc"
        into "src/main/resources/static/docs"
    }
}

tasks.build {
    dependsOn tasks.copyDocument
}

tasks.jar {
    dependsOn tasks.copyDocument
    enabled = true
}
