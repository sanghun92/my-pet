plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

dependencies {
    // spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // db
    implementation 'mysql:mysql-connector-java'

    // modules
    implementation project(':my-pet-config')
    implementation project(':my-pet-domain')

    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
    testImplementation 'com.google.guava:guava:31.1-jre'

    // to fix [javax.annotation.meta.When not found]
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // to fix [ Could not write JSON: Java 8 date/time type `java.time.LocalDateTime` not supported]
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

asciidoctor.doLast {
    copy {
        from file("build/docs/asciidoc")
        into file("src/main/resources/static/docs")
    }
}

build {
    dependsOn asciidoctor
}

jar {
    dependsOn asciidoctor

    from(asciidoctor.outputDir) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        into("static/docs")
    }
}
